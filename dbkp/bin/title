#!/usr/bin/env fish

argparse --name=title 'w/wall=' 's#size' -- $argv
and argparse --min-args=1 -- $argv
or begin
  echo "Usage: title [--wall=C|-wc] [-80] text"
  echo "  Prints a title with C as char for the walls and containing text."
  echo "  -80 sets the size of the frame. Defaults to \$COLUMNS up to at most 80."
  return
end

set text (string join ' ' $argv)
set text_len (string length $text)

set wall '#'
if test -n "$_flag_wall" 
  set wall (string sub -l1 $_flag_wall)
end

set screen_size 80
if test -n "$_flag_size"
  set screen_size $_flag_size
else if test -n "$COLUMNS"
  set screen_size (math "min(80,$COLUMNS)")
end

if test $text_len -gt (math $screen_size-4)
  set screen_size (math $text_len+6)
end

set padding_left (math "round(($screen_size-4-$text_len)/2)")
set padding_right (math $screen_size-$padding_left-$text_len-4)

echo (string repeat -n $screen_size $wall)
echo "$wall$wall$(string repeat -n (math $screen_size-4) ' ')$wall$wall"
echo "$wall$wall$(string repeat -n $padding_left ' ')$text$(string repeat -n $padding_right ' ')$wall$wall"
echo "$wall$wall$(string repeat -n (math $screen_size-4) ' ')$wall$wall"
echo (string repeat -n $screen_size $wall)
